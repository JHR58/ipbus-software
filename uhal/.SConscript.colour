## Sconstruct file for adding colour to the SCONs output
##   @author Andrew W. Rose
##   @date 11/27/2010

import os
import sys

## Import the environment from the Cactus SConscript File
Import('env')

AddOption("--readable",action="store_true", dest="readable_flag",default=False,help="readable output") 
AddOption("--no-colour",action="store_true", dest="no_colour_flag",default=False,help="get the standard SCons output") 

# user doesn't get colour if they don't want it
if not GetOption("no_colour_flag"): 
	#If the output is not a terminal, no chance of the colors
	if sys.__stdout__.isatty():
		#windows does not support ANSI colours
		if env['PLATFORM'] != 'win32':
			env['ENV']['ColorizePath'] = sys.path[0]+"/.colorize"
			env['ENV']['PATH'] = env['ENV']['ColorizePath'] + ":" + env['ENV']['PATH']

			#if the readable flag is not set, use space as a delimeter, else, use newline and tab indentation
			delimeter = ' '
			delimeter2 = ' '
			if GetOption("readable_flag"): 
				delimeter='\n\t'	
				delimeter2='\t'	
			
			#make a map of colour names to the escape codes
			colors = {}
			colors['cyan']   = '\033[1;36m'
			colors['purple'] = '\033[1;35m'
			colors['blue']   = '\033[1;34m'
			colors['green']  = '\033[1;32m'
			colors['yellow'] = '\033[1;33m'
			colors['red']    = '\033[0;31m'
			colors['end']    = '\033[0m'
			colors['white']  = '\033[1;37m'

			colors['bluebg']  = '\033[44;37m'


			#define some colourful messages 
			compile_source_message = '%sCompiling%s : %s$SOURCE%s\n' 			% (colors['blue'], colors['end'], colors['red'], colors['end'])
			compile_shared_source_message = '%sCompiling shared%s : %s$SOURCE%s\n'		% (colors['purple'], colors['end'], colors['red'], colors['end'])
			link_program_message = '%sLinking Program%s : %s$TARGET%s\n' 			% (colors['cyan'], colors['end'], colors['red'], colors['end'])
			link_library_message = '%sLinking Static Library%s : %s$TARGET%s\n' 		% (colors['green'], colors['end'], colors['red'], colors['end'])
			ranlib_library_message = '%sRanlib Library%s : %s$TARGET%s\n' 			% (colors['white'], colors['end'], colors['red'], colors['end'])
			link_shared_library_message = '%sLinking Shared Library%s : %s$TARGET%s\n'	% (colors['yellow'], colors['end'], colors['red'], colors['end'])
			install_message = '%sInstall $SOURCE as $TARGET %s' % ( colors['bluebg'], colors['end'] )


			#define some stings for concatenation into the output messages
			_CPPINCFLAGS = '${_concat(INCPREFIX, CPPPATH, INCSUFFIX, __env__, RDirs, TARGET, SOURCE)}'
			_CPPDEFFLAGS = '${_defines(CPPDEFPREFIX, CPPDEFINES, CPPDEFSUFFIX, __env__)}'
			_CCCOMCOM = '$CPPFLAGS '+ _CPPDEFFLAGS + ' ' + _CPPINCFLAGS	
			_LIBDIRFLAGS = '${_concat(LIBDIRPREFIX, LIBPATH, LIBDIRSUFFIX, __env__, RDirs, TARGET, SOURCE)}'
			_LIBFLAGS = '${_stripixes(LIBLINKPREFIX, LIBS, LIBLINKSUFFIX, LIBPREFIXES, LIBSUFFIXES, __env__)}'

			#define the messages in the actual environment
			env['CXXCOMSTR']= compile_source_message\
						+ '$CXX'+delimeter2\
						+ '-o $TARGET'+delimeter\
						+ '-c'+delimeter\
						+ '$CXXFLAGS'+delimeter\
						+ '$CCFLAGS'+delimeter\
						+ _CCCOMCOM+delimeter\
						+ '$SOURCES'

			env['CCCOMSTR'] = compile_source_message\
						+ '$CC'+delimeter2\
						+ '-o $TARGET'+delimeter\
						+ '-c'+delimeter\
						+ '$CFLAGS'+delimeter\
						+ '$CCFLAGS'+delimeter\
						+ _CCCOMCOM+delimeter\
						+ '$SOURCES'
			env['SHCCCOMSTR'] = compile_shared_source_message\
						+ '$SHCC'+delimeter2\
						+ '-o $TARGET'+delimeter\
						+ '-c'+delimeter\
						+ '$SHCFLAGS'+delimeter\
						+ '$SHCCFLAGS'+delimeter\
						+ _CCCOMCOM+delimeter\
						+ '$SOURCES'
			env['SHCXXCOMSTR'] = compile_shared_source_message\
						+ '$SHCXX'+delimeter2\
						+ '-o $TARGET'+delimeter\
						+ '-c'+delimeter\
						+ '$SHCXXFLAGS'+delimeter\
						+ '$SHCCFLAGS'+delimeter\
						+ _CCCOMCOM+delimeter\
						+ '$SOURCES'
			env['ARCOMSTR'] = link_library_message\
						+ '$AR $ARFLAGS $TARGET $SOURCES'
			env['RANLIBCOMSTR'] = ranlib_library_message\
						+ 'ranlib'
			env['SHLINKCOMSTR'] = link_shared_library_message\
						+ '$SHLINK'+delimeter2\
						+ '-o $TARGET'+delimeter\
						+ '$SHLINKFLAGS'+delimeter\
						+ '$SOURCES'+delimeter\
						+ _LIBDIRFLAGS+delimeter\
						+ _LIBFLAGS
			env['LINKCOMSTR'] = link_program_message\
						+ '$LINK'+delimeter2\
						+ '-o $TARGET'+delimeter\
						+ '$LINKFLAGS'+delimeter\
						+ '$SOURCES'+delimeter\
						+ _LIBDIRFLAGS+delimeter\
						+ _LIBFLAGS

			env['INSTALLSTR'] = install_message
